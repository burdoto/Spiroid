version = parent.version

apply plugin: 'java-library'
apply from: "vars.gradle"

task buildPluginApi(type: Copy, dependsOn: 'jar') {
    from tasks.findByName('jar').outputs.files.singleFile
    into file("$parent.projectDir/out/api")
    rename { String filename -> return "spiroid-api@${project.version}.jar" }
}

tasks.withType(Javadoc) {
    source = sourceSets.main.java
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

dependencies {
    api project(':api')
    api project(':guardian:uniform')

    runtimeOnly('com.google.flogger:flogger-log4j-backend:0.5.1') {
        exclude group: 'com.sun.jmx', module: 'jmxri'
        exclude group: 'com.sun.jdmk', module: 'jmxtools'
        exclude group: 'javax.jms', module: 'jms'
    }
}

apply from: 'publishing.gradle'
