import java.util.concurrent.TimeUnit

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: 'gradle/vars.gradle'

group 'org.comroid'
version '0.4.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(Javadoc) {
    source = sourceSets.main.java
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

processResources {
    from(sourceSets.main.resources) {
        expand project.properties
    }
}

task copyPluginForTesting(type: Copy, dependsOn: ['shadowJar', 'publishToMavenLocal']) {
    from shadowJar.outputs.files.singleFile
    into file("$testServerDir/plugins/")
    doFirst {
        System.out.println("Output Directory: " + outputs.files.singleFile)
    }
}

repositories {
    maven { url 'https://maven.comroid.org' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:+'
    compileOnly "org.spigotmc:spigot-api:$mcVersion-$spigotRC-SNAPSHOT"

    if (isAutoDeploy) {
        api 'org.comroid:uniform:+'
    } else {
        api project(':guardian:uniform')
    }
}

apply from: 'gradle/publishing.gradle'
